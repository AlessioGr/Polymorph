plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Ladysnake Libs"
        url = "https://dl.bintray.com/ladysnake/libs"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation fabricApi.module("fabric-api-base", project.fabric_version)
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-networking-api-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-command-api-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-commands-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-networking-v0", project.fabric_version)

    modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${project.cca_version}"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems:${project.rei_version}"
    modRuntime "me.shedaniel:RoughlyEnoughItems:${project.rei_version}"
    modCompileOnly "curse.maven:fabricfurnaces-315534:${project.file_fabricfurnaces}"
    modRuntime "curse.maven:fabricfurnaces-315534:${project.file_fabricfurnaces}"
    modCompileOnly "curse.maven:ironfurnaces-318036:${project.file_ironfurnaces}"
    modRuntime "curse.maven:ironfurnaces-318036:${project.file_ironfurnaces}"
    modRuntime "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    //    modRuntime "curse.maven:fastfurnace-364540:3023434"

    include fabricApi.module("fabric-api-base", project.fabric_version)
    include fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    include fabricApi.module("fabric-networking-v0", project.fabric_version)
    include fabricApi.module("fabric-networking-api-v1", project.fabric_version)
    include fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
    include fabricApi.module("fabric-command-api-v1", project.fabric_version)
    include fabricApi.module("fabric-commands-v0", project.fabric_version)
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${project.cca_version}"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

sourceSets {
    main {
        resources {
            srcDirs += 'docs'
        }
    }
}

curseforge {
    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('docs/CHANGELOG.md')
        addGameVersion "Java 8"
        addGameVersion "Fabric"
        addGameVersion project.minecraft_version
        addArtifact(sourcesJar)
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))

        relations {
            embeddedLibrary 'fabric-api'
            embeddedLibrary 'cardinal-components'
        }

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
